buildscript {
    dependencies {
        // see https://developer.android.com/studio/releases/gradle-plugin
        // section "Android Gradle plugin and Android Studio compatibility"
        // which Gradle plugin version we can use
        // with our specific Android Studio version
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

plugins {
    id 'com.android.application' version '7.3.1' apply true
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply true
}

android {
    namespace 'de.jurihock.aubergine'
    compileSdkVersion 33

    defaultConfig {
        applicationId 'de.jurihock.aubergine'
        versionName '3.0'
        versionCode 13
        minSdkVersion 27 // Oboe + AAudio
        targetSdkVersion 33
        ndk {
            // restrict ABIs as supplied by Oboe
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                // restrict ABIs as supplied by Oboe
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                // force shared library variant of libc++ as required by Oboe
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }

    buildTypes {
        release {
            // enables code shrinking, obfuscation, and optimization
            minifyEnabled false
            // enables resource shrinking
            shrinkResources false
        }
    }

    buildFeatures {
        // enable import of prefab dependencies as required by Oboe
        prefab true
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/de/jurihock/aubergine/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.oboe:oboe:1.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
}
